buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

if (System.getenv().DRONE_BUILD_NUMBER)
    version += "." + System.getenv().DRONE_BUILD_NUMBER
group = "mcjty.rftoolsdim" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "rftoolsdim"

sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    mappings = mcp_mappings
    useDepAts = true
    makeObfSourceJar = false
}

repositories {
    maven { url "https://dvs1.progwml6.com/files/maven" } // JEI
    maven { url "https://maven.mcmoddev.com/" } // Hwyla
    maven { url "https://maven.tterrag.com/" } // TheOneProbe / Chisel
    maven { url "https://maven.covers1624.net/" } // RedstoneFlux
    maven { url "https://maven.blamejared.com/" } // Tesla
    maven { url "https://maven.k-4u.nl/" } // intwheel / mcjtylib / rftools
    maven { url "https://maven.cil.li/" } // OpenComputers
    maven {
        url "https://cursemaven.com"
    }
}

dependencies {

    deobfProvided "mezz.jei:jei_${jei_version}"
    deobfProvided "mcjty.theoneprobe:TheOneProbe-${theoneprobe_version}"
    deobfProvided "cofh:RedstoneFlux:${redstoneflux_version}:universal"
    deobfProvided "net.darkhax.tesla:Tesla-${tesla_version}"
    deobfProvided "com.github.mcjty:intwheel:${intwheel_version}"
    deobfProvided "li.cil.oc:OpenComputers:${opencomputers_version}"
    deobfProvided "team.chisel:Chisel:${chisel_version}"

    if (findProject(":Hwyla") != null) compile project(":Hwyla")
    else deobfProvided "mcp.mobius.waila:Hwyla:${hwyla_version}"

//    deobfCompile "com.github.mcjty:mcjtylib:${mcjtylib_version}"
    deobfCompile "curse.maven:mcjtylib-233105:2745846"

    if (findProject(":RFTools") != null) compile project(":RFTools")
    else deobfCompile "curse.maven:rftools-224641:2861573"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

tasks.register("deobfJar", Jar) {
    from sourceSets.main.output
    classifier = "dev"
}

artifacts {
    archives sourceJar
    archives deobfJar
}

jar {
    manifest {
        attributes "FMLAT": "rftoolsdim_at.cfg"
    }
}
